

plugins {
    id 'com.gradleup.shadow' version '9.2.2'
    id 'application'
}


application {
    mainClass.set('mqtt.timer.MqttTimerMain')
}


ext.buildCommit    = 'git describe --always --tags --dirty=-dirty'.execute().text.trim()
ext.buildTimestamp = java.time.format.DateTimeFormatter.ISO_INSTANT.format(java.time.Instant.now())
ext.buildJdk       = "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})"

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}


dependencies {
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.+'
    implementation 'org.slf4j:slf4j-api:2.+'
    implementation 'org.apache.logging.log4j:log4j-core:2.+'
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.+'
}


shadowJar {
    manifest {
        from('src/main/resources/META-INF/MANIFEST.MF')
        attributes(
            'Specification-Title':   project.name,
            'Specification-Version': '1.0.0',
            'Implementation-Title':  project.name,
            'Build-Revision':        buildCommit,
            'Build-Timestamp':       buildTimestamp,
            'Build-Jdk':             buildJdk
            )
    }
}
