

plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'application'
}


application {
    mainClass.set('mqtt.timer.MqttTimerMain')
}


ext.buildCommit    = 'git describe --always --tags --dirty=-dirty'.execute().text.trim()
ext.buildTimestamp = java.time.format.DateTimeFormatter.ISO_INSTANT.format(java.time.Instant.now())
ext.buildJdk       = "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})"

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}


dependencies {
    implementation group: 'org.eclipse.paho',         name: 'org.eclipse.paho.client.mqttv3', version: '1.2.+'
    implementation group: 'org.slf4j',                name: 'slf4j-api',                      version: '2.+'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core',                     version: '2.+'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j2-impl',              version: '2.+'
}


shadowJar {
    manifest {
        from('src/main/resources/META-INF/MANIFEST.MF')
        attributes(
            'Specification-Title':   project.name,
            'Specification-Version': '1.0.0',
            'Implementation-Title':  project.name,
            'Build-Revision':        buildCommit,
            'Build-Timestamp':       buildTimestamp,
            'Build-Jdk':             buildJdk
            )
    }
}

